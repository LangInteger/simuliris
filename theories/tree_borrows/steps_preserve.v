From iris.prelude Require Import prelude options.
From stdpp Require Export gmap.
From simuliris.tree_borrows Require Import lang_base notation bor_semantics tree tree_lemmas bor_lemmas.
From iris.prelude Require Import options.

Lemma access_eqv_strict_rel t t' (tr tr':tree item) :
  forall fn cids tg range dyn_rel,
  app_preserves_tag fn ->
  tree_apply_access fn cids tg range tr dyn_rel = Some tr' ->
  StrictParentChildIn t t' tr <-> StrictParentChildIn t t' tr'.
Proof.
  move=> ????? Preserves Access.
  eapply join_map_eqv_strict_rel; [|exact Access].
  move=> ??.
  apply Preserves.
Qed.

Lemma access_eqv_rel t t' (tr tr':tree item) :
  forall fn cids tg range dyn_rel,
  app_preserves_tag fn ->
  tree_apply_access fn cids tg range tr dyn_rel = Some tr' ->
  ParentChildIn t t' tr <-> ParentChildIn t t' tr'.
Proof.
  move=> ????? Preserves Access.
  unfold ParentChildIn.
  rewrite access_eqv_strict_rel; [|exact Preserves|exact Access].
  tauto.
Qed.

Lemma item_apply_access_preserves_tag kind :
  app_preserves_tag (item_apply_access kind).
Proof.
  move=> ??? it it'.
  unfold item_apply_access.
  destruct (permissions_foreach); simpl; [|intro H; inversion H].
  intro H; injection H; intros; subst.
  simpl; reflexivity.
Qed.

Lemma access_preserves_tags tr tg :
  forall tr' tg' app cids range dyn_rel,
  app_preserves_tag app ->
  tree_apply_access app cids tg' range tr dyn_rel = Some tr' ->
  tree_contains tg tr <-> tree_contains tg tr'.
Proof.
  move=> tr' tg' app ??? Preserve Access.
  unfold tree_apply_access in Access.
  unfold tree_contains in *.
  rewrite (join_project_exists _ _ tr').
  2: exact Access.
  rewrite exists_node_map.
  unfold compose.
  pose proof (proj1 (join_success_condition (map_nodes _ tr)) (mk_is_Some _ _ Access)).
  rewrite every_node_map in H.
  split; intro Contains.
  * eapply exists_node_increasing.
    - exact Contains.
    - unfold compose in H.
      rewrite every_node_eqv_universal.
      rewrite every_node_eqv_universal in H.
      intros x Exists Tagx.
      pose proof (H x Exists) as Hspec.
      destruct Hspec as [v App].
      exists v.
      split; auto.
      unfold IsTag in *; subst.
      symmetry.
      apply (Preserve _ _ _ _ _ App).
  * eapply exists_node_increasing.
    - exact Contains.
    - rewrite every_node_eqv_universal.
      intros x Exists xspec.
      destruct xspec as [v App].
      destruct App.
      unfold IsTag in *; subst.
      apply (Preserve _ _ _ _ _ H0).
Qed.

Lemma insertion_preserves_tags tr tg :
  forall tgp tgc cids tr' newp,
  tree_contains tg tr ->
  create_child cids tgp tgc newp tr = Some tr' ->
  tree_contains tg tr'.
Proof.
  move=> ??? tr' ? Contains CreateChild.
  unfold tree_contains in *.
  pose proof (create_child_isSome tr _ _ _ _ _ CreateChild) as Insert.
  rewrite Insert.
  apply insert_preserves_exists.
  exact Contains.
Qed.

Lemma apply_access_spec_per_node tr affected_tag access_tag pre:
  tree_contains access_tag tr ->
  tree_contains affected_tag tr ->
  tree_unique affected_tag tr pre ->
  forall fn cids range tr' dyn_rel,
  app_preserves_tag fn ->
  tree_apply_access fn cids access_tag range tr dyn_rel = Some tr' ->
  exists post,
    Some post = fn cids (if dyn_rel pre.(itag) access_tag then AccessChild else AccessForeign) range pre
    /\ tree_contains affected_tag tr'
    /\ tree_unique affected_tag tr' post.
Proof.
  intros Contains Contains' TgSpec fn cids range tr' dyn_rel FnPreservesTag Success.
  (* Grab the success condition of every node separately *)
  pose proof (proj1 (join_success_condition _) (mk_is_Some _ _ Success)) as SuccessCond.
  rewrite every_node_map in SuccessCond; rewrite every_node_eqv_universal in SuccessCond.
  pose proof (exists_unique_exists _ _ _ Contains' TgSpec) as Expre.
  pose proof (SuccessCond pre Expre) as [post SpecPost].
  unfold tree_unique in TgSpec. rewrite every_node_eqv_universal in TgSpec.
  (* Now do some transformations to get to the node level *)
  unfold tree_unique.
  exists post.
  split; [symmetry; auto|].
  split; [rewrite <- (access_preserves_tags _ _ _ _ _ _ _ _ FnPreservesTag Success); exact Contains'|].
  rewrite join_project_every; [|exact Success].
  rewrite every_node_map.
  unfold compose.
  rewrite every_node_eqv_universal.
  intros n Exn.
  destruct (decide (IsTag affected_tag n)).
  * pose proof (TgSpec n Exn) as PerNodeEqual.
    clear Success Contains SuccessCond TgSpec Contains' Exn.
    exists post.
    split; [|tauto].
    rewrite PerNodeEqual; auto.
  * pose proof (SuccessCond n Exn) as NodeSuccess.
    destruct NodeSuccess as [post' post'Spec].
    exists post'.
    unfold IsTag; rewrite <- (FnPreservesTag _ _ _ _ _ post'Spec).
    split; [|tauto].
    exact post'Spec.
Qed.

Lemma bor_estep_preserves_contains tg trs blk :
  trees_contain tg trs blk ->
  forall trs' cids cids' evt,
  bor_estep
    trs cids
    evt
    trs' cids'
  ->
  trees_contain tg trs' blk.
Proof.
  move=> Ex ???? Step.
  inversion Step; subst.
  - destruct (decide (blk = x.1)); [subst|].
    * rewrite /trees_contain FRESH_BLOCK in Ex; contradiction.
    * rewrite /trees_contain /extend_trees; rewrite lookup_insert_ne; [|done]; exact Ex.
  - destruct (option_bind_success_step _ _ _ ACC) as [?[H ACC']]; clear ACC.
    destruct (option_bind_success_step _ _ _ ACC') as [?[H' ACC'']]; clear ACC'.
    injection ACC''; intros; subst; clear ACC''.
    destruct (decide (blk = x.1)); [subst|].
    * rewrite /trees_contain lookup_insert.
      rewrite /trees_contain H in Ex.
      rewrite <- access_preserves_tags; [exact Ex|apply item_apply_access_preserves_tag|exact H'].
    * rewrite /trees_contain; rewrite lookup_insert_ne; [|done]; exact Ex.
  - destruct (option_bind_success_step _ _ _ ACC) as [?[H ACC']]; clear ACC.
    destruct (option_bind_success_step _ _ _ ACC') as [?[H' ACC'']]; clear ACC'.
    injection ACC''; intros; subst; clear ACC''.
    destruct (decide (blk = x.1)); [subst|].
    * rewrite /trees_contain lookup_insert.
      rewrite /trees_contain H in Ex.
      rewrite <- access_preserves_tags; [exact Ex|apply item_apply_access_preserves_tag|exact H'].
    * rewrite /trees_contain; rewrite lookup_insert_ne; [|done]; exact Ex.
  - destruct (option_bind_success_step _ _ _ ACC) as [?[H ACC']]; clear ACC.
    destruct (option_bind_success_step _ _ _ ACC') as [?[H' ACC'']]; clear ACC'.
    injection ACC''; intros; subst; clear ACC''.
    destruct (decide (blk = x.1)); [subst|].
Abort.

(* FIXME: this needs some well-formedness
Lemma bor_step_alloc_prouces_contains.
Lemma bor_step_preserves_contains.
Lemma bor_step_preserves_unique.
Lemma bor_step_preserves_rel.
*)
